-- Stack machine.

import Array;
import List;
import Fun;
import Collection;
import World;
import State;
import Expr;
import Buffer;

-- Stack code printer. Takes a list of SM instructions, return its
-- string representation.
public fun showSMInsn (i) {
  case i of
    READ         -> sprintf ("READ")
  | WRITE        -> sprintf ("WRITE")
  | BINOP (s)    -> sprintf ("BINOP %s", s)
  | LD    (x)    -> sprintf ("LD %s", x)
  | ST    (x)    -> sprintf ("ST %s", x)
  | CONST (n)    -> sprintf ("CONST %d", n)
  | LABEL (s)    -> sprintf ("LABEL %s", s)
  | JMP   (l)    -> sprintf ("JMP %s", l)
  | CJMP  (c, l) -> sprintf ("CJMP %s, %s", c, l)
  esac
}

public fun showSM (prg) {
  map (fun (i) {showSMInsn (i) ++ "\n"}, prg).stringcat
}

-- Evaluation environment: keeps a mapping between labels and (sub)programs
fun initEvalEnv (insns) {
  var map =
    fix (fun (rec) {
          fun ([m, insns]) {
            case insns of
              {}               -> m
            | LABEL (lab) : tl -> rec ([addMap (m, lab, insns), tl])
            | _ : tl           -> rec ([m, tl])
            esac
          }
        }) $ [emptyMap (compare), insns];
  
  [fun (l) {
     case findMap (map, l) of Some (insns) -> insns esac
  }]
}

-- Accessor function
fun fromLabel (env, lab) {
  env [0] (lab)
}

fun cjmpCond (type, z) {
  case type of
    "z" -> z == 0
  | "nz" -> z != 0
  esac
}



fun evalI(env, c@[st, s, w@[in, out]], instr:insns) {
  case instr of
    READ      -> case readWorld(w) of
                  [n, w] -> [[n:st, s, w], insns]
                  esac
  | WRITE     -> case st of
                  x:st -> [[st, s, writeWorld(x, w)], insns]
                  esac
  | BINOP (op) -> case st of
                  x:y:st -> [[getOp(op, y, x) : st, s, w], insns]
                  esac
  | LD    (x) -> [[s(x) : st, s, w], insns]
  | ST    (x) -> case st of
                  n:st -> [[st, s <- [x, n], w], insns]
                  esac
  | CONST (n) -> [[n:st, s, w], insns]
  | LABEL (_) -> [c, insns]
  | JMP (l) -> [c, env.fromLabel (l)]
  | CJMP (type, l) ->
      case st of
        x : newSt ->
          if cjmpCond (type, x)
          then [[newSt, s, w], env.fromLabel (l)]
          else [[newSt, s, w], insns]
          fi
      esac
  esac
}

-- Stack machine interpreter. Takes an environment, an SM-configuration and a program,
-- returns a final configuration
fun eval (env, c, insns) {
  case insns of
    {} -> c
  | _  -> 
      case evalI (env, c, insns) of
        [c, insns_] -> eval (env, c, insns_)
      esac
  esac
}

-- Runs a stack machine for a given input and a given program, returns an output
public fun evalSM (input, insns) {
  eval (initEvalEnv (insns), [{}, emptyState, createWorld (input)], insns)[2].getOutput
}

-- Compilation environment: generates labels
fun makeCompEnv (n) {
  [fun () {
     [sprintf ("L%d", n), makeCompEnv (n+1)]
  }]
}

fun initCompEnv () {
  makeCompEnv (0)
}

-- Accessor function: generates one label
fun genLabel (env) {
  env [0] ()
}

-- Utility function: generates n labels
fun genLabels (env, n) {
  fun inner (env, n) {
    if n == 0
    then {env}
    else case env.genLabel of
           [lab, env] -> lab : inner (env, n-1)
         esac
    fi
  }

  listArray (inner (env, n))
}

-- Compiles an expression into a stack machine code.
-- Takes an expression, returns a list of stack machine instructions
fun compileExpr (expr) {
   case expr of
    Var (y) -> singletonBuffer(LD (y))
  | Const (n) -> singletonBuffer(CONST (n))
  | Binop (op, e1, e2) -> compileExpr(e1) <+> compileExpr(e2) <+ BINOP(op)
  esac
}

-- Compiles a statement into a stack machine code.
-- Takes a statement, returns a list of stack machine
-- instructions.
public fun compileSM (stmt) {
  -- Generates a buffer with an optional label
  --    lab     --- the name of the label
  --    labUsed --- a flag if the lebel is needed
  fun label (lab, labUsed) {
    if labUsed
    then singletonBuffer (LABEL (lab))
    else emptyBuffer ()
    fi
  }

  -- Generates SM code for a given statement
  --    lab  --- a label th go to after the statement is executed
  --    env  --- compilation environment
  --    stmt --- a statement to generate SM code for
  -- Returns a triple:
  --    boolean flag indicating if "lab" was used
  --    updated environment
  --    code buffer  
  fun compile (lab, env, stmt) {
    case stmt of
      Skip           -> [false, env, emptyBuffer ()]
    | Read  (x)      -> [false, env, singletonBuffer (READ) <+ ST (x)]
    | Write (e)      -> [false, env, compileExpr (e) <+ WRITE]
    | Assn  (x, e)   -> [false, env, compileExpr (e) <+ ST (x)]
    | Seq   (s1, s2) ->
        case env.genLabel of
          [s2Lab, env] ->
             case compile (s2Lab, env, s1) of
               [s2LabUsed, env, s1Code] ->
                  case compile (lab, env, s2) of
                    [labUsed, env, s2Code] -> [labUsed, env, s1Code <+> label (s2Lab, s2LabUsed) <+> s2Code]
                  esac
             esac
        esac
    | If (e, s1, s2) ->
      case env.genLabels(2) of 
        [label1, label2, env] -> 
          case compile (label1, env, s1) of
            [label1Used, env, code1] -> 
              case compile (lab, env, s2) of
                [_, env, code2] -> [
                  true, env, 
                  compileExpr(e) <+> 
                  singletonBuffer (CJMP("z", label2)) <+> 
                  code1 <+> 
                  label (label1, label1Used) <+> 
                  listBuffer ({JMP(lab), LABEL (label2)}) <+> 
                  code2
                ]
              esac
          esac
      esac
    | While (e, s) -> 
      case env.genLabels(2) of
        [label1, label2, env] -> 
          case compile (label2, env, s) of
            [_, env, code] -> [
              false, env, 
              singletonBuffer (JMP (label2)) <+ LABEL(label1) <+> code <+ LABEL (label2)
                <+> compileExpr (e) <+ CJMP ("nz", label1)
            ]
          esac
      esac

    | DoWhile (s, e) -> 
      case env.genLabels(2) of
        [label1, label2, env] -> 
          case compile (label2, env, s) of
            [labUsed, env, code] -> [
              false, env, 
              singletonBuffer (LABEL(label1)) <+ code <+ label (label2, labUsed)
                <+> compileExpr (e) <+ CJMP ("nz", label1)
            ]
          esac
      esac
    esac
  }  

  case initCompEnv ().genLabel of
    [endLab, env] ->
       case compile (endLab, env, stmt) of
         [endLabUsed, _, code] -> getBuffer $  code <+> label (endLab, endLabUsed)
       esac
  esac
}
